###
### STATE ELABORATION/INFERENCES:
### LARGER, UPPER-DISK, AND CLEAR
###  These are relations and properties used by other rules

sp {towers-of-hanoi*object*disk*elaborate*state*larger
   "If size-of(i) > size-of(j), then larger(i,j)."
   (state <s> ^name towers-of-hanoi
              ^disk <i-disk> { <> <i-disk> <j-disk> })
   (<i-disk> ^size <i-size>)
   (<j-disk> ^size <j-size> > <i-size>)
-->
   (<j-disk> ^larger <i-disk>)}
    
sp {towers-of-hanoi*object*disk*elaborate*state*upper-disk
   "If not(above(x,disk)), then upper-disk(disk)."
   (state <s> ^name towers-of-hanoi
              ^disk <disk>)
  -{ (<s> ^holds.above <disk>) }
-->
   (<s> ^upper-disk <disk>)}

sp {towers-of-hanoi*object*peg*elaborate*state*clear
   "If not(on(x,peg)), then clear(peg)."
   (state <s> ^name towers-of-hanoi
              ^peg <peg>)
  -{ (<s> ^holds.on <peg>) }
-->
   (<s> ^clear <peg>)}