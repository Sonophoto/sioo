

###
### TOWER-OF-HANOI PROBLEM SPACE: 
### MONITOR STATE AND OPERATORS
###

sp {tower-of-hanoi*monitor*state*holds*above*disk
    (state <s> ^superstate nil
               ^holds <h>)
    (<h> ^disk.size <u-size> 
         ^above.size <w-size>
         ^on.name <peg-name>)
    -->
    (write (crlf) 
           |  On(| <u-size> 
           |,| <peg-name> 
           |),Above(| <w-size>
           |).|)}
  
sp {tower-of-hanoi*monitor*state*holds*above*none
    (state <s> ^superstate nil
               ^holds <h>)
    (<h> ^disk.size <size>
         ^above none 
         ^on.name <peg-name>)
    -->
    (write (crlf) 
           |  On(| <size> 
           |,| <peg-name> 
           |).|)}
  
sp {tower-of-hanoi*monitor*state*upper-disk
    (state <s> ^superstate nil
               ^upper-disk.size <size>)
    -->
    (write (crlf) 
           |  Upper-Disk(| <size> 
           |).|)}
  
sp {tower-of-hanoi*monitor*state*clear
    (state <s> ^superstate nil
               ^clear.name <name>)
    -->
    (write (crlf) 
           |  Clear(Peg(| <name> 
           |)).|)}
  

sp {tower-of-hanoi*desired-state*above*disk
    (state <s> ^desired <d> 
               ^superstate nil)
    (<d> ^disk <u-disk> ^above <w-disk> ^on <peg>)
    (<u-disk> ^size <u-size>)
    (<w-disk> ^size <w-size>)
    (<peg> ^name <peg-name>)
    -->
    (write (crlf) 
           |  Desired/On(| <u-size> 
           |,| <peg-name> 
           |),Above(| <u-size>
           |,| <w-size>
           |)/.|)}
  
sp {tower-of-hanoi*desired-state*above*none
    (state <s> ^desired <d> ^superstate nil)
    (<d> ^disk <disk> ^above none ^on <peg>)
    (<disk> ^size <size>)
    (<peg> ^name <peg-name>)
    -->
    (write (crlf) 
           |  Desired[On(| <size> 
           |,| <peg-name> 
           |),Above(| <size>
           |,-)].|)}
  
sp {tower-of-hanoi*monitor*operator-execution*move-disk
    (state <s> ^operator <o>)
    (<o> ^name move-disk
         ^disk.size <size>
         ^from.name <from-name>
         ^to.name <to-name>)
    -->
    (write (crlf) 
           |  Executing(Move-Disk(| <size>
           |,| <from-name> 
           |,| <to-name>
           |)).|)}

### eof of tower-of-hanoi.soar

