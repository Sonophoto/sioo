###
### Initialize the state
###

sp {blocks-world*propose*initialize
   (state <s> ^name blocks-world
             -^object)
-->
   (<s> ^operator <o>)
   (<o> ^name initialize)
}

## Modify the following rule to change the initial state or the goal.
## It is necessary for the initial state and the desired state to share the same
## identifiers for the objects (<blockA>, <blockB>, <blockC>, <table>) because of
## the way the goal is detected (in elaborations/detect-success).

sp {blocks-world*apply*initialize
   (state <s> ^operator.name initialize)
-->
   (<s> ^ontop <ontop1> <ontop2> <ontop3>
        ^object <blockA> <blockB> <blockC> <table>
        ^desired <ds>)
   (<ontop1> ^top-block <blockA>
             ^bottom-block <blockB>)
   (<ontop2> ^top-block <blockB>
             ^bottom-block <table>)
   (<ontop3> ^top-block <blockC>
             ^bottom-block <table>)
   (<blockA> ^name A ^type block)
   (<blockB> ^name B ^type block)
   (<blockC> ^name C ^type block)
   (<table> ^name table ^type table)
   (write (crlf) |Simple Blocks World - just move blocks.|)
   (write (crlf) |Initial state has A on B, B on table, and C on the table.|)
   (<ds> ^ontop <dontop1> <dontop2> <dontop3>)
   (<dontop1> ^top-block <blockA>
              ^bottom-block <blockB>)
   (<dontop2> ^top-block <blockB>
              ^bottom-block <blockC>)
   (<dontop3> ^top-block <blockC>
              ^bottom-block <table>)
   (write (crlf) |The goal is to get A on B on C on the table.|)}