sp {e*top-state*top-state
	(state <s> ^superstate nil)
 -->
	(<s> ^top-state <s>)}

sp {e*top-state*sub-state
	(state <s> ^superstate.top-state <ts>)
-->
	(<s> ^top-state <ts>)}

sp {e*biggest-disk
	(state <s> ^io.input-link <il>)
	(<il> ^disk.size <sz> -^disk.size > <sz>)
-->
	(<s> ^biggest-disk <sz>)}

sp {e*clear
	(state <s> ^io.input-link <il>)
	(<il> ^disk.size <sz> -^disk.above <sz>)
  -->
	(<s> ^clear <sz>)}

sp {e*upper-disk*some-disk
	(state <s> ^io.input-link <il>)
	(<il> ^disk <d1> -^disk.above <sz>)
	(<d1> ^size <sz> ^on <p>)
	-->
	(<s> ^upper-disk <u>)
	(<u> ^peg <p> ^size <sz>)}

sp {e*upper-disk*no-disk
	(state <s> ^io.input-link <il> ^biggest-disk <bdsz>)
	(<il> ^peg <p> -^disk.on <p>)
 -->
	(<s> ^upper-disk <u>)
	(<u> ^peg <p> ^size (+ <bdsz> 10))}

	

sp { p*clear*biggest-not-on-goal
	(state <s> ^io.input-link <il>)
 	(<il> ^goal-peg <p> ^disk <d>)
 	(<d> ^size <sz> ^on <> <p>)
	-{(<il> ^disk <d2>)
  	  (<d2> ^size > <sz> ^on <> <p>)}
 -->
	(<s> ^operator <o> + <)
	(<o> ^name clear-disk ^disk <sz> ^to-move-to <p>)}


##  If we want to clear a disk, and can do it primitively, 
##  clear the disk that is above it first.

sp { p*clear*disk-above-disk-being-cleared
	(state <s> ^top-state.io.input-link <il> ^superstate.operator <sso>)
	(<sso> ^name clear-disk ^disk <sz> ^to-move-to <gp>)
	(<il> ^disk <dtc> ^disk <thisd> ^peg { <p> <> <gp> <> <origp>})
	(<dtc> ^size <sz> ^on <origp>)
	(<thisd> ^size <thissz> ^on <origp> ^above <sz>)
	-->
	(<s> ^operator <o> + )
	(<o> ^name clear-disk ^disk <thissz> ^to-move-to <p>)}


sp { p*move-disk*clear
	(state <s> ^superstate.operator <sso> ^top-state <ts>)
	(<sso> ^name clear-disk ^disk <d> ^to-move-to <p>)
	(<ts> ^clear <d> ^upper-disk <ud>)
	(<ud> ^peg <p> ^size > <d>)
	-->
	(<s> ^operator <o> + )
	(<o> ^name move-disk ^command <cmd>)
	(<cmd> ^move-disk <m>)
	(<m> ^size <d> ^to <p>)}

sp { p*clear-disk*clear-target-peg
	(state <s> ^superstate.operator <sso> ^top-state <ts>)
	(<sso> ^name clear-disk ^disk <cd> ^to-move-to <p>)
	(<ts> ^io.input-link <il>)
	(<il> ^disk <d> ^disk <od> ^peg <dp>)
	(<od> ^size <cd> ^on { <op> <> <dp>} )
	(<d> ^on {<p> <> <dp>}  ^size { <dsz> < <cd>})
	-{(<il> ^disk <d2>)
	  (<d2> ^on <p> ^size {> <dsz> < <cd>})}
	-->
	(<s> ^operator <o> + )
	(<o> ^name clear-disk ^disk <dsz> ^to-move-to <dp>)}
	

sp {a*copy-command*body
	(state <s> ^top-state.io.output-link <ol> ^operator <o>)
	(<o> ^command <cmd>)
	(<cmd> ^<cmd-name> <temp>)
 -->
	(<ol> ^<cmd-name> <c>)}

sp {a*copy-command*args
	(state <s> ^top-state.io.output-link <ol> ^operator <o>)
	(<ol> ^<cmd-name> <c>)
	(<c> -^status complete)
	(<o> ^command <cmd>)
	(<cmd> ^<cmd-name> <link>)
	(<link> ^<arg> <value>)
 -->
	(<c> ^<arg> <value>)}
		

sp {a*remove*complete
	(state <s> ^operator <o> ^io.output-link <ol>)
	(<o> ^name <n>)
	(<ol> ^<cmd> <link>)
	(<link> ^status complete)
  -->
  	(<ol> ^<cmd> <link> - )}


sp {e*goal-acheived
	(state <s> ^io.input-link <il>)
	(<il> ^goal-peg <p> -^disk.on <> <p>)
-->
	(write (crlf) |Success!| (crlf))
	(halt)}	

sp {a*maintain
	(state <s> ^operator <o>)
 -->
	(<s> ^operator <o> >)}
